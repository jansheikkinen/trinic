# Grammar
A grammar specification of the Unholy C language using Extended Backus-Naur
Form.

```ebnf
basetype    =  "int8"   | "int16"   | "int32"   | "int64"   | "isize"
             | "uint8"  | "uint16"  | "uint32"  | "uint64"  | "usize"
             | "float8" | "float16" | "float32" | "float64" | "fsize"
             | "bool" ;
arraytype   = type "[" expression "]" ;
pointertype = type "*" ;
structtype  = "struct" IDENTIFIER structbody*? ;
structbody  = vardecl* "end" ;
type        = basetype | arraytype | pointertype | structtype ;


statement   = (assignment | vardecl | conditional) "." ;
vardecl     = type assignment ;
assignment  = IDENTIFIER "=" (expression | "undefined") ;
conditional = "if" expression "do" statement*
              ( "end" | "else" (statement* "end" | conditional) ) ;


expression = logic ;
logic      = equality ( ("and" | "or")                  equality )*  ;
equality   = compare  ( ("==" | "!=")                   compare  )*  ;
compare    = bitwise  ( (">" | ">=" | "<" | "<=")       bitwise  )*  ;
bitwise    = term     ( ("&" | "|" | "^" | "<<" | ">>") term     )*  ;
term       = factor   ( ("+" | "-")                     factor   )*  ;
factor     = unary    ( ("*" | "/")                     unary    )*  ;
unary      =            ("~" | "!" | "not" | "-")       unary | call ;
call       = primary "(" arguments? ")" ;
primary    = "(" expression ")" | NUMBER | STRING | IDENTIFIER ;

arguments = expression ( "," expression )* ;
```
